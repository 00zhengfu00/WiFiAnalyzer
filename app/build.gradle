/*
 *    Copyright (C) 2015 - 2016 VREM Software Development <VREMSoftwareDevelopment@gmail.com>
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

apply plugin: 'com.android.application'

android {
    def version = updateVersion();
    compileSdkVersion 22
    buildToolsVersion "23.0.2"
    defaultConfig {
        applicationId "com.vrem.wifianalyzer"
        minSdkVersion 22
        targetSdkVersion 22
        versionCode version.versionCode
        versionName version.versionName
    }
    buildTypes {
        release {
            minifyEnabled true
        }
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

dependencies {
    // Compile Build Dependencies
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.jjoe64:graphview:4.0.1'
    // Unit Test Dependencies
    testCompile 'junit:junit:4.12'
    testCompile 'org.powermock:powermock-module-junit4:1.6.4'
    testCompile 'org.powermock:powermock-api-mockito:1.6.4'
    testCompile 'com.googlecode.junit-toolbox:junit-toolbox:2.2'
    testCompile 'org.robolectric:robolectric:3.1-SNAPSHOT'
}

class Version {
    int versionCode
    String versionName
}

def Version updateVersion() {
    def propertiesFilename = 'build.properties'
    def propertiesFile = file(propertiesFilename)
    if (propertiesFile.canRead()) {
        def Properties properties = new Properties()
        properties.load(new FileInputStream(propertiesFile))
        def versionMajor = properties['version_major'].toString().toInteger()
        def versionMinor = properties['version_minor'].toString().toInteger()
        def versionPatch = properties['version_patch'].toString().toInteger()
        def versionBuild = properties['version_build'].toString().toInteger()
        def versionStore = properties['version_store'].toString().toInteger()

        def taskNames = gradle.getStartParameter().getTaskNames()
        if ('assemble' in taskNames || 'assembleRelease' in taskNames || 'aR' in taskNames) {
            versionPatch++
            versionStore++
            properties['version_patch'] = versionPatch.toString()
            properties['version_store'] = versionStore.toString()
        }
        versionBuild++
        properties['version_build'] = versionBuild.toString()
        properties.store(propertiesFile.newWriter(), "Build Properties")

        def versionName = "${versionMajor}.${versionMinor}.${versionPatch} (${versionBuild})"
        return new Version(versionCode: versionStore, versionName: versionName)
    } else {
        throw new GradleException("Could not read " + propertiesFilename + " file!")
    }
}
