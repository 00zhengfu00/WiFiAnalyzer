/*
 *    Copyright (C) 2015 - 2016 VREM Software Development <VREMSoftwareDevelopment@gmail.com>
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

apply plugin: 'com.android.application'

android {
    def keystoreInfo = getKeystoreInfo()
    signingConfigs {
        config {
            keyAlias keystoreInfo.keyAlias
            keyPassword keystoreInfo.keyPassword
            storeFile file(keystoreInfo.storeFilename)
            storePassword keystoreInfo.storePassword
        }
    }

    compileSdkVersion 22
    buildToolsVersion "23.0.2"

    def version = updateVersion()
    defaultConfig {
        applicationId "com.vrem.wifianalyzer"
        minSdkVersion 22
        targetSdkVersion 22
        versionCode version.versionCode
        versionName version.versionName
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
        debug {
            debuggable true
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    // Compile Build Dependencies
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.jjoe64:graphview:4.0.1'
    // Unit Test Dependencies
    testCompile 'junit:junit:4.12'
    testCompile 'org.powermock:powermock-module-junit4:1.6.4'
    testCompile 'org.powermock:powermock-api-mockito:1.6.4'
    testCompile 'com.googlecode.junit-toolbox:junit-toolbox:2.2'
    testCompile 'org.robolectric:robolectric:3.1-SNAPSHOT'
}

class Version {
    int versionCode
    String versionName
}

def Version updateVersion() {
    def propertiesFile = file('build.properties')
    def Properties properties = readProperties(propertiesFile)

    def versionMajor = properties['version_major'].toString().toInteger()
    def versionMinor = properties['version_minor'].toString().toInteger()
    def versionPatch = properties['version_patch'].toString().toInteger()
    def versionBuild = properties['version_build'].toString().toInteger()
    def versionStore = properties['version_store'].toString().toInteger()

    def taskNames = gradle.getStartParameter().getTaskNames()
    if (':app:assembleRelease' in taskNames) {
        System.out.println(">>> Building Release Version...")
        versionPatch++
        versionStore++
        properties['version_patch'] = versionPatch.toString()
        properties['version_store'] = versionStore.toString()
    }
    versionBuild++
    properties['version_build'] = versionBuild.toString()

    writeProperties(propertiesFile, properties)

    def versionName = "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"
    System.out.println(">>> " + project.parent.name + " Version Name: " + versionName + " Code: " + versionStore)
    return new Version(versionCode: versionStore, versionName: versionName)
}

def Properties readProperties(propertiesFile) {
    if (propertiesFile.canRead()) {
        def Properties properties = new Properties()
        def inputStream = new FileInputStream(propertiesFile)
        properties.load(inputStream)
        inputStream.close()
        return properties
    } else {
        def message = "Could not read " + propertiesFile.name + " file!"
        System.err.println(message)
        throw new GradleException(message)
    }
}

def writeProperties(propertiesFile, properties) {
    def writer = propertiesFile.newWriter()
    properties.store(writer, "Build Properties")
    writer.close()
}

class KeystoreInfo {
    String keyAlias
    String keyPassword
    String storeFilename
    String storePassword
}

def KeystoreInfo getKeystoreInfo() {
    def propertiesFile = file('../../keystores/androidkeystore.properties')
    def Properties properties = readProperties(propertiesFile)

    def keyAlias = properties['key_alias'].toString()
    def keyPassword = properties['key_password'].toString()
    def storeFilename = properties['store_filename'].toString()
    def storePassword = properties['store_password'].toString()

    return new KeystoreInfo(
            keyAlias: keyAlias,
            keyPassword: keyPassword,
            storeFilename: storeFilename,
            storePassword: storePassword
    )
}